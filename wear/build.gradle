apply plugin: 'com.android.application'

// idea borrowed from here: http://stackoverflow.com/questions/17097263/automatically-versioning-android-project-from-git-describe-with-android-studio-g
def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}


android {
    compileSdkVersion 20
    buildToolsVersion "21.0.2"

    defaultConfig {
        applicationId "org.dwallach.calwatch"
        minSdkVersion 20
        targetSdkVersion 20
        versionCode getVersionCode()
        versionName getVersionName()
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':shared')

    // compile 'com.google.android.gms:play-services:5.2.08'
    compile 'com.google.android.gms:play-services-wearable:+'
    compile 'com.google.android.support:wearable:+'
    // compile 'com.squareup.wire:wire-runtime:1.5.1'
}
