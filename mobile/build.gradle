apply plugin: 'com.android.application'

// idea borrowed from here: http://stackoverflow.com/questions/17097263/automatically-versioning-android-project-from-git-describe-with-android-studio-g

def getVersionCode = { ->
    def versionCode = -1

    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }

        versionCode = Integer.parseInt(stdout.toString().trim())

        println "version code: " + versionCode

    }
    catch (ignored) { }

    return versionCode
}

def getVersionName = { ->
    def versionName = "noName"
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }

        versionName = stdout.toString().trim()

        println "version name: " + versionName
    }
    catch (ignored) { }

    return versionName
}


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.dwallach.calwatch"
        minSdkVersion 18      // Android 4.3, Jelly Bean, which is the minimum for the Android Wear app
        targetSdkVersion 22   // Android 5.1, Lollipop, which is the current Android Wear system version
        versionCode getVersionCode()
        versionName getVersionName()
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':shared')
    wearApp project(':wear')     // says that this project has both the mobile and wear components

    // compile 'com.android.support:appcompat-v7:+'
    // compile 'com.google.android.gms:play-services:5.2.08'
    // compile 'com.google.android.gms:play-services-wearable:+'
    compile 'com.google.android.gms:play-services:6.1.+'
    // compile 'com.google.android.gms:play-services-all-wear:+'
    // compile 'com.android.support:appcompat-v7:+'
    // compile 'com.squareup.wire:wire-runtime:1.5.1'
}
