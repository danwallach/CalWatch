apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// idea borrowed from here: http://stackoverflow.com/questions/17097263/automatically-versioning-android-project-from-git-describe-with-android-studio-g

def getVersionCode = { ->
    def versionCode = -1

    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }

        versionCode = Integer.parseInt(stdout.toString().trim())

        println "version code: " + versionCode

    }
    catch (ignored) { }

    return versionCode
}

def getVersionName = { ->
    def versionName = "noName"
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }

        versionName = stdout.toString().trim()

        println "version name: " + versionName
    }
    catch (ignored) { }

    return versionName
}

buildscript {
    ext.kotlin_version = '1.0.2-eap-74'
    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/kotlin/kotlin-eap/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "org.dwallach.calwatch"
        minSdkVersion 18      // Android 4.3, Jelly Bean, which is the minimum for the Android Wear app
        targetSdkVersion 23   // Android 6.0, Marshmallow, with the shiny new permission model
        versionCode getVersionCode()
        versionName getVersionName()
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
        }
    }
}

// http://stackoverflow.com/questions/18689365/how-to-add-xlintunchecked-to-my-android-gradle-based-project
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':shared')
    wearApp project(':wear')     // says that this project has both the mobile and wear components

    compile 'com.google.android.gms:play-services-wearable:8.4.0'
    compile 'com.google.android.gms:play-services-fitness:8.4.0'
    compile 'fr.nicolaspomepuy.androidwearcrashreport:crashreport-mobile:0.6@aar'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.anko:anko-sdk15:0.8.3"
    compile "com.android.support:cardview-v7:23.4.0"
    compile "com.android.support:support-v4:23.4.0"
    compile "com.android.support:appcompat-v7:23.4.0"
}
repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/kotlin/kotlin-eap/'
    }
}
