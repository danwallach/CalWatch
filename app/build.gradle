apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// idea borrowed from here: http://stackoverflow.com/questions/17097263/automatically-versioning-android-project-from-git-describe-with-android-studio-g
def getVersionCode = { String apk ->
    def versionCode = -1

    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }

        // Note the use of string concatenation, then convert to an Integer later on; this ensures
        // that when Git's version number grows an extra digit, everything here continues working
        // correctly, i.e., using the apk target number as a prefix.
        versionCode = apk + stdout.toString().trim()

        println "version code: " + versionCode
    }
    catch (ignored) {
    }

    return Integer.parseInt(versionCode)
}

def getVersionName = { ->
    def versionName = "noName"
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }

        versionName = stdout.toString().trim()

        println "version name: " + versionName
    }
    catch (ignored) {
    }

    return versionName
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'org.dwallach.calwatch2'
        targetSdkVersion 28
//        minSdkVersion 25
        minSdkVersion 23
        multiDexEnabled true
        versionName getVersionName()
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishNonDefault true

    productFlavors {
        wear1 {
            // Use the defaultConfig value
            dimension "normal"
            versionCode = getVersionCode("23")
        }
        wear2 {
            dimension "normal"
            minSdkVersion 25
            versionCode = getVersionCode("25")
        }
    }

    flavorDimensions "normal"
}
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.android.support:wearable:2.4.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    compileOnly 'com.google.android.wearable:wearable:2.4.0'

    implementation 'com.google.android.gms:play-services-wearable:16.0.1'

    implementation 'androidx.core:core-ktx:1.0.1'
    implementation 'androidx.wear:wear:1.0.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko-commons:0.10.8"
}

// https://stackoverflow.com/questions/42374151/all-com-android-support-libraries-must-use-the-exact-same-version-specification
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0'
            }
        }
    }
}
